; Script generated by the HM NIS Edit Script Wizard.
SetCompressor lzma

; MUI 1.67 compatible ------
!include "MUI2.nsh"
!include "FileFunc.nsh"

!include "ZipDLL.nsh"
!include "EnvVarUpdate.nsh"

; ------------ TextTest settings ---------
!define PRODUCT_NAME "TextTest"
!ifndef PRODUCT_VERSION
  !define PRODUCT_VERSION ""
!endif
!define PRODUCT_PUBLISHER "TextTest"
!define PRODUCT_WEB_SITE "http://www.texttest.org"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\AppMainExe.exe"

Var TT_HOME
Var ROOT_DRIVE
Var PYTHON_PATH
Var JYTHON_PATH
Var VIRTUALENV_PATH
Var TKDIFF_PATH
Var WINMERGE_PATH

!define env_hkcu 'HKCU "Environment"'
!define env_hklm 'HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"'
!define TT_BIN "source\bin"

!ifdef WIN64
  !define ARCH "win64"
!else
  !define ARCH "win32"
!endif

!ifdef JEPPESEN
  !define CENTRAL_PYUSECASE_LOCATION "T:\texttest\release\current.win\pyusecase"
  !define CENTRAL_TEXTTEST_LOCATION "T:\texttest\release\current.win"
!else
  !ifndef TEXTTEST_DIST
    !define TEXTTEST_DIST "texttest-3.21.zip"
  !endif
  !ifndef TEXTTEST_ROOT
    !define TEXTTEST_ROOT "texttest-3.21"
  !endif
!endif

!ifndef VIRTUALENV_DIST
  !define VIRTUALENV_DIST "virtualenv.py"
!endif
!ifndef PYTHON_VERSION
  !DEFINE PYTHON_VERSION "2.6"
!endif
!ifndef JYTHON_VERSION
  !DEFINE JYTHON_VERSION "2.5.2"
!endif

!ifndef VIRTUAL_PYTHON
  !define VIRTUAL_PYTHON "python${PYTHON_VERSION}"
!endif
!ifndef VIRTUAL_JYTHON
  !define VIRTUAL_JYTHON "jython${JYTHON_VERSION}"
!endif

!ifndef PYTHON_INSTALLER
  !define PYTHON_INSTALLER "python-2.6.6.msi"
!endif
!ifndef JYTHON_INSTALLER
  !define JYTHON_INSTALLER "jython_installer-2.5.2.jar"
!endif
!ifndef PYGTK_INSTALLER
  !define PYGTK_INSTALLER "pygtk-all-in-one-2.22.6.win32-py2.6.msi"
!endif

!ifndef TKDIFF_INSTALLER
  !define TKDIFF_INSTALLER "tkdiffInstall.exe"
!endif

!ifndef WINMERGE_INSTALLER
  !define WINMERGE_INSTALLER "WinMerge-2.12.4-exe.zip"
!endif

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME

; Components page
!insertmacro MUI_PAGE_COMPONENTS

; Directory page
!ifndef JEPPESEN
LangString TEXT_TOP ${LANG_ENGLSH} "Setup will install \
Texttest in the following folder..."
!define MUI_DIRECTORYPAGE_TEXT_TOP "$(TEXT_TOP)"
;!DEFINE MUI_DIRECTORYPAGE_VARIABLE $INSTDIR
!insertmacro MUI_PAGE_DIRECTORY
!endif

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Texttest home page
PageEx directory
  DirVerify leave
  DirText "Select a root directory to store TextTest tests. This will set the environment variable TEXTTEST_HOME."
  DirVar $TT_HOME
PageExEnd
; Finish page
!insertmacro MUI_PAGE_FINISH

; Language files
!insertmacro MUI_LANGUAGE "English"
; Reserve files
;!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
BrandingText ""
RequestExecutionLevel user
Caption "${PRODUCT_NAME}"
!ifdef JEPPESEN
  OutFile "texttext-all-in-one${PRODUCT_VERSION}.${ARCH}.jeppesen.exe"
!else
  OutFile "texttext-all-in-one${PRODUCT_VERSION}.${ARCH}.exe"
!endif
InstallDir "C:\Texttest"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show


Section -SETTINGS
  SetOutPath $TEMP\texttest
  SetOverwrite ifnewer
  Call initialize
  ifErrors 0 +2
  Abort
SectionEnd

Section "Python" SEC02
  ClearErrors
  File "${PYTHON_INSTALLER}"
  ExecWait '"msiexec" /i "$OUTDIR\${PYTHON_INSTALLER}" ALLUSERS=1 /passive' $0
  IfErrors onError
  Call installVirtualEnvOnPython
  IfErrors onError
  Call updatePythonPath
  IfErrors onError done
  onError:
    Abort
  done:
SectionEnd

Section "PyGtk" SEC03
  File "${PYGTK_INSTALLER}"
  ExecWait '"msiexec" /i "$OUTDIR\${PYGTK_INSTALLER}"'
  IfErrors 0 +2
    Abort
SectionEnd

SectionGroup /e "Diff tool" G1
  Section "Tkdiff" g1o1
    File "${TKDIFF_INSTALLER}"
    ExecWait "$OUTDIR\tkdiffInstall.exe"
    IfErrors onError
    Call updateTkDiffPath
    IfErrors onError done
    onError:
    Abort
    done:
  SectionEnd
  Section /o "WinMerge" g1o2
    File "${WINMERGE_INSTALLER}"
    ZipDLL::extractall "$OUTDIR\${WINMERGE_INSTALLER}" "$ROOT_DRIVE\"
    IfErrors onError
    Call updateWinMergePath
    IfErrors onError done
    onError:
    Abort
    done:
  SectionEnd
SectionGroupEnd

!ifdef JEPPESEN
Section "TextTest and PyUseCase configuration" SEC06
  Call configurePyusecase
  IfErrors onError
  Call updateTexttestPath
  IfErrors onError done
  onError:
    Abort
  done:
SectionEnd
!else
Section "PyUseCase for Python GUI testing" SEC07
  Call configurePyusecasePython
  IfErrors onError done
  onError:
    Abort
  done:
SectionEnd

Section "PyUseCase for Java GUI testing" SEC08
  Call installJython
  IfErrors onError
  Call configurePyusecaseJava
  IfErrors onError done
  onError:
    Abort
  done:
SectionEnd

Section "Texttest" SEC09
  ;SectionIn RO
  ZipDLL::extractall "$OUTDIR\${TEXTTEST_DIST}" "$INSTDIR"
  IfErrors onError
  Call updateTexttestPath
  IfErrors onError done
  onError:
    Abort
  done:
SectionEnd
!endif

Section "Uninstall"
  Call un.install
SectionEnd

Function initialize
  ${GetRoot} "$PROGRAMFILES" $ROOT_DRIVE
  StrCpy $INSTDIR "$ROOT_DRIVE\Texttest"
  StrCpy $JYTHON_PATH "$ROOT_DRIVE\jython${JYTHON_VERSION}"
  StrCpy $VIRTUALENV_PATH "$ROOT_DRIVE\virtualenv"
  StrCpy $TKDIFF_PATH "$PROGRAMFILES\TkDiff"
  StrCpy $WINMERGE_PATH "$ROOT_DRIVE\WinMerge-2.12.4-exe"
  File "${VIRTUALENV_DIST}"
  !ifndef JEPPESEN
    File "open_source\${TEXTTEST_DIST}"
  !endif
  !ifdef JEPPESEN
    writeUninstaller "$OUTDIR\uninstall.exe"
  !else
    CreateDirectory "$INSTDIR"
    writeUninstaller "$INSTDIR\uninstall.exe"
  !endif
  Call copyVirtualEnvFiles
FunctionEnd

Function copyVirtualEnvFiles
  CreateDirectory "$VIRTUALENV_PATH"
  IfErrors onError
  CopyFiles /SILENT "$OUTDIR\${VIRTUALENV_DIST}" $VIRTUALENV_PATH
  IfErrors onError done
  onError:
    Abort
  done:
FunctionEnd

Function installJython
  File "${JYTHON_INSTALLER}"
  ExecWait 'java -jar $OUTDIR\${JYTHON_INSTALLER} -s -d "$JYTHON_PATH"'
  IfErrors onError
  Call installVirtualEnvOnJython
  IfErrors onError
  Call updateJythonPath
  IfErrors onError done
  onError:
    Abort
  done:
FunctionEnd

Function .onInstSuccess
  Call setTexttestHome
FunctionEnd

Function setTexttestHome
  WriteRegStr ${env_hkcu} "TEXTTEST_HOME" $TT_HOME
  DetailPrint $TT_HOME
FunctionEnd

Function updateTexttestPath
  !ifdef JEPPESEN
    ${EnvVarUpdate} $0 "PATH" "P" "HKCU" "${CENTRAL_PYUSECASE_LOCATION}\bin"
    ${EnvVarUpdate} $0 "PATH" "P" "HKCU" "${CENTRAL_TEXTTEST_LOCATION}\bin"
  !else
    ${EnvVarUpdate} $0 "PATH" "P" "HKCU" "$INSTDIR\${TEXTTEST_ROOT}\${TT_BIN}"
  !endif
FunctionEnd

Function updatePythonPath
  ${EnvVarUpdate} $0 "PATH" "P" "HKCU" "$VIRTUALENV_PATH\${VIRTUAL_PYTHON}\bin"
  ${EnvVarUpdate} $0 "PATH" "P" "HKCU" "$VIRTUALENV_PATH\${VIRTUAL_PYTHON}\Scripts"
FunctionEnd

Function updateJythonPath
  ${EnvVarUpdate} $0 "PATH" "P" "HKCU" "$VIRTUALENV_PATH\${VIRTUAL_JYTHON}\bin"
FunctionEnd

Function updateTkDiffPath
  ${EnvVarUpdate} $0 "PATH" "P" "HKCU" "$TKDIFF_PATH"
FunctionEnd

Function updateWinMergePath
  ${EnvVarUpdate} $0 "PATH" "P" "HKCU" "$WINMERGE_PATH"
FunctionEnd

Function configurePyusecasePython
  !ifdef JEPPESEN
    ExecWait '"cmd.exe" /K CD $VIRTUALENV_PATH & ECHO Configuring pyusecase on python & ${VIRTUAL_PYTHON}\Scripts\pip install -e ${CENTRAL_PYUSECASE_LOCATION} & EXIT'
    IfErrors onError done
  !else
    ExecWait '"cmd.exe" /K CD $VIRTUALENV_PATH & ECHO Configuring pyusecase on python & ${VIRTUAL_PYTHON}\Scripts\pip install pyusecase & EXIT'
    IfErrors onError done
  !endif
  onError:
    MessageBox MB_OK|MB_ICONSTOP "Python is not installed. Run the installer again and choose Python"
    Quit
  done:
FunctionEnd

Function configurePyusecaseJava
  !ifdef JEPPESEN
    ExecWait '"cmd.exe" /K CD $VIRTUALENV_PATH & ECHO Configuring pyusecase on jython & ${VIRTUAL_JYTHON}\bin\jython ${VIRTUAL_JYTHON}\bin\pip install -e ${CENTRAL_PYUSECASE_LOCATION} && EXIT'
    IfErrors onError done
  !else
    ;ExecWait '"cmd.exe" /K CD $VIRTUALENV_PATH & ECHO Configuring pyusecase on jython & ${VIRTUAL_JYTHON}\bin\jython ${VIRTUAL_JYTHON}\bin\pip install pyusecase && EXIT'
    ExecWait '"cmd.exe" /K CD $VIRTUALENV_PATH & ECHO Configuring pyusecase on jython & ${VIRTUAL_JYTHON}\bin\jython ${VIRTUAL_JYTHON}\bin\easy_install pyusecase && EXIT'
    IfErrors onError done
  !endif
  onError:
    Abort
  done:
FunctionEnd

Function getPythonPath
  ReadRegStr $PYTHON_PATH HKCU "SOFTWARE\Python\PythonCore\${PYTHON_VERSION}\InstallPath" ""
  IfErrors 0 done
  ReadRegStr $PYTHON_PATH HKLM "SOFTWARE\Python\PythonCore\${PYTHON_VERSION}\InstallPath" ""
  IfErrors onError done
  onError:
    MessageBox MB_OK|MB_ICONSTOP "Python is not installed"
    Abort
  done:
FunctionEnd

Function installVirtualEnvOnPython
  Call getPythonPath
  ExecWait '"cmd.exe" /K CD $VIRTUALENV_PATH & ECHO installing virtualenv on python & "$PYTHON_PATH"python virtualenv.py ${VIRTUAL_PYTHON} && EXIT'
Functionend

Function installVirtualEnvOnJython
  ;ExecWait '"cmd.exe" /K CD $VIRTUALENV_PATH & ECHO installing virtualenv on jython & "$PYTHON_PATH"python virtualenv.py -p $JYTHON_PATH\bin\jython.bat ${VIRTUAL_JYTHON} && EXIT'
  ExecWait '"cmd.exe" /K CD $VIRTUALENV_PATH & ECHO installing virtualenv on jython & $JYTHON_PATH\bin\jython virtualenv.py ${VIRTUAL_JYTHON} && EXIT'
Functionend

Function un.install
  RMDir /r $JYTHON_PATH
  RMDir /r "$OUTDIR"
  RMDir /r $VIRTUALENV_PATH
  Call un.setEnv
  Delete "$INSTDIR\Uninstaller.exe"
FunctionEnd

Function un.setEnv
  DeleteRegKey "HKCU" "Environment\TEXTTEST_HOME"
  !ifdef JEPPESEN
    ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "${CENTRAL_PYUSECASE_LOCATION}\bin"
    ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "${CENTRAL_TEXTTEST_LOCATION}\bin"
  !else
    ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "$INSTDIR\texttest-3.21\${TT_BIN}"
  !endif
  ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "$VIRTUALENV_PATH\${VIRTUAL_PYTHON}\bin"
  ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "$VIRTUALENV_PATH\${VIRTUAL_PYTHON}\Scripts"
  ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "$VIRTUALENV_PATH\${VIRTUAL_JYTHON}\bin"
  ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "$TKDIFF_PATH"
  ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "$WINMERGE_PATH"
FunctionEnd

Function .onInit
  StrCpy $1 ${g1o1}
FunctionEnd
/*
Function .onSelChange
  !insertmacro StartRadioButtons $1
    !insertmacro RadioButton ${g1o1}
    !insertmacro RadioButton ${g1o2}
  !insertmacro EndRadioButtons
FunctionEnd
*/
